
&Дуб
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

&Завязь
&Характер("Компанейский")
Функция Массив(&Блестяшка Размер, &Блестяшка Элементы, &Блестяшка Фиксированный) Экспорт
	Если Размер > 0 Тогда
		Массив = Новый Массив(Размер);
	Иначе
		Массив = Новый Массив();
	КонецЕсли;

	Счетчик = 0;
	Для Каждого ТекЭлемент из Элементы Цикл
		Значение = ТекЭлемент.Параметры[0].Значение;
		Если Размер > 0 и Счетчик <= Размер - 1 Тогда
			Массив[Счетчик] = Значение;
		Иначе
			Массив.Добавить(Значение);
		КонецЕсли;	
		Счетчик = Счетчик + 1;
	КонецЦикла;

	Если Фиксированный = Истина Тогда
		Массив = Новый ФиксированныйМассив(Массив);
	КонецЕсли;

	Возврат Массив;
КонецФункции

&Завязь
&Характер("Компанейский")
Функция Структура(&Блестяшка Ключи, &Блестяшка КлючиИЗначения, &Блестяшка Фиксированный) Экспорт
	Структура = Новый Структура(Ключи);

	Для Каждого КлючИЗначение из КлючиИЗначения Цикл
		Если КлючИЗначение.Параметры.Количество() = 1 Тогда
			Структура.Вставить(КлючИЗначение.Параметры[0].Значение);
		ИначеЕсли КлючИЗначение.Параметры.Количество() > 1 Тогда
			Структура.Вставить(КлючИЗначение.Параметры[0].Значение, КлючИЗначение.Параметры[1].Значение);
		Иначе
			// Ничего
		КонецЕсли;
	КонецЦикла;
	
	Если Фиксированный = Истина Тогда
		Структура = Новый ФиксированнаяСтруктура(Структура);
	КонецЕсли;

	Возврат Структура;
КонецФункции

&Завязь
&Характер("Компанейский")
Функция Соответствие(&Блестяшка КлючиИЗначения, &Блестяшка Фиксированный) Экспорт
	Соответствие = Новый Соответствие();

	Для Каждого КлючИЗначение из КлючиИЗначения Цикл
		Если КлючИЗначение.Параметры.Количество() = 1 Тогда
			Соответствие.Вставить(КлючИЗначение.Параметры[0].Значение);
		ИначеЕсли КлючИЗначение.Параметры.Количество() > 1 Тогда
			Соответствие.Вставить(КлючИЗначение.Параметры[0].Значение, КлючИЗначение.Параметры[1].Значение);
		Иначе
			// Ничего
		КонецЕсли;
	КонецЦикла;
	
	Если Фиксированный = Истина Тогда
		Соответствие = Новый ФиксированноеСоответствие(Соответствие);
	КонецЕсли;

	Возврат Соответствие;
КонецФункции

&Завязь
&Характер("Компанейский")
Функция ТаблицаЗначений(&Блестяшка КолонкиСтрокой, &Блестяшка Колонки, &Блестяшка Строки) Экспорт
	ТаблицаЗначений = Новый ТаблицаЗначений();

	Если НЕ ПустаяСтрока(КолонкиСтрокой) Тогда
		Для каждого ИмяКолонки Из СтрРазделить(КолонкиСтрокой, ",") Цикл
			ТаблицаЗначений.Колонки.Добавить(СокрЛП(ИмяКолонки));	
		КонецЦикла;
	КонецЕсли;

	Для Каждого ТекКолонка из Колонки Цикл
		ИмяКолонки = ТекКолонка.Параметры[0].Значение;
		Если ТаблицаЗначений.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
			ТаблицаЗначений.Колонки.Добавить(СокрЛП(ИмяКолонки));
		КонецЕсли
	КонецЦикла;

	Для Каждого ТекСтрока из Строки Цикл
		НоваяСтрока = ТаблицаЗначений.Добавить();
		Для Каждого ТекПараметр из ТекСтрока.Параметры Цикл
			НоваяСтрока[ТекПараметр.Имя] = ТекПараметр.Значение;
		КонецЦикла;
	КонецЦикла;

	Возврат ТаблицаЗначений;
КонецФункции